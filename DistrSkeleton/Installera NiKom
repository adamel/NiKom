; Start på funktioner som används i programmet.

(procedure P_Addtoconfigfile configfile parameter
	(textfile
		(dest configfile)
		(include configfile)
		(append
			("%s\n" parameter)
		)
	)
)

(procedure P_AddCommandstokommandoncfg

	(set destfile "NiKom:Datocfg/Kommandon.cfg")

	(textfile
		(dest destfile)
		(include destfile)
		(append
			("\n* Adderat av NiKom v1.60:s installationsprogram\n")
		)
	)

	; Här börjar installering av kommandona...

	(P_InstallCommand "SYSINFO" "0" "10" "508" "Uppdaterat Sysinfo kommando.")
	(P_InstallCommand "SORTERA MÖTEN" "0" "100" "520" "Kommando för att enklare sortera upp möteslistan.")
	(P_InstallCommand "SPARA" "1" "10" "328" "Sparar alla olästa och dylikt utan att logga ut personen.")
	

	; Här slutar installeringen av kommandona...

	(textfile
		(dest destfile)
		(include destfile)
		(append
			("\n* Slut på kommandon adderade av NiKom v1.60:s installationsprogram\n")
		)
	)
)


(procedure P_InstallCommand ProgramName Flaggor Status Nummer Description

	(set AntOrd
		(if (patmatch "#? #?" ProgramName)
			("2")
			
			("1")
		)
	)

	(if (not
			(askchoice
				(prompt ("Vill du installera kommandot %s (se hjälpen för mer info)" ProgramName))
				(help ("%s" Description))
				(choices "JA" "NEJ")
				(default 0)
			)
	    )

		(
			(textfile
				(help
					("Detta kommer att addderas till Kommandon.cfg\n")
					("N=%s\n" ProgramName)
					("S=%s\n" Status)
					("O=%s\n" AntOrd)
					("#=%s\n\n" Nummer)
					("Dessutom så kommer ExtKom%s.nik att kopieras till NiKom:Rexx\n" Nummer)
				)
				(prompt ("Installerar kommandot %s" ProgramName))
				(dest destfile)
				(include destfile)
				(append
					("\nN=%s\n" ProgramName)
					("S=%s\n" Status)
					("O=%s\n" AntOrd)
					("#=%s\n" Nummer)
				)
			)
	
			(if (= flaggor 0)
				(
					(copyfiles
						(prompt ("%s kommandot är uppdaterat, vill du installera det?" ProgramName))
						(dest ("%srexx" dirnamewslash))
    			   		(source "rexx")
			    	   	(pattern ("ExtKom%s.nik" Nummer))
		    		   	(help ("%s" Description))
	    		   	)
				)
			)
	
			(copyfiles
				(prompt ("%s kommandot är uppdaterat, vill du installera manualen?" ProgramName))
				(dest ("%sManualer" dirnamewslash))
	   			(source "manualer")
				(pattern ("%s.man" Nummer))
				(help ("%s" Description))
			)
		
		)

	)
)


(procedure P_CopyConfigFiles
	(
		(copyfiles
			(prompt "Vart ska NiKoms konfigurations filer kopieras?")
			(dest ("%sDatocfg" dirnamewslash))
			(source "Datocfg")
			(pattern "#?")
			(help "Detta bör accepteras annars kommer inte NiKom att fungera!")
		)

		(copyfiles
			(prompt "Vart ska NiKoms text filer kopieras? (inloggstexten, utloggstexten, etc.)")
			(dest ("%sTexter" dirnamewslash))
			(source "Texter")
			(pattern "#?")
			(help "Detta bör accepteras annars kommer inte NiKom att fungera!")
		)

		(copyfiles
			(prompt "Vart ska NiKoms ARexx program kopieras?")
			(dest ("%sRexx" dirnamewslash))
			(source "Rexx")
			(pattern "#?.nik")
			(help "Detta bör accepteras annars kommer inte NiKom att fungera ordentligt!")
		)
	)
)

(procedure P_Endofinstallation
	(
		(if (exists ("%sdoc/NiKom.guide" dirnamewslash))
			(run
				("sys:utilities/multiview %sdoc/NiKom.guide" dirnamewslash)
				(prompt "Vill du läsa NiKoms manual med bla. information om vad som är nytt i NiKom v1.60prerel1? (kräver att du har multiview installerat)")
				(help "Här kan du hitta information om hur du gör för att få igång din BBS och mycket annat.")
				(confirm)
			)
		)
	)
)

(procedure P_NewInstallation
	(
		(set startupstring "")

		(if (= NiKomexists 0)
			(
				(set startupstring
					("assign NiKom: %s" dirname)
				)
			)
		)

		(startup "NiKom"
			(prompt "För att NiKom ska fungera riktigt så måste en katalog med namn ram:NiKom skapas innan NiKoms startas upp, vill du göra det nu?")
			(help "Gör du inte detta nu så måste du göra det själv!")
			(command ("%s\nmakedir RAM:NiKom\nPATH NiKom:Bin add" startupstring))
		)

		(if (askbool
				(prompt "Vill du installera FIFO")
				(help "FIFO används för att köra externa applikationer från NiKom, så det rekommenderas att du installerar FIFO.")
			)

		  (
			(copylib
				(prompt "Fifo.library krävs för att kunna köra externa applikationer från NiKom")
				(help   "Behövs inte, installera!")
				(source "libs/fifo.library")
				(dest   "libs:")
			)

			(copylib
				(prompt "Fifo-handler krävs också för att kunna köra externa applikationer från NiKom")
				(help   "Behövs inte, installera!")
				(source "l/fifo-handler")
				(dest   "l:")
			)

			(startup "FIFO"
				(prompt "För att Fifo:n ska fungera så behöver måste en rad läggas till i s:user-startup, vill du göra detta? (för att detta ska få effekt så måste du starta om datorn)")
				(help "FIFO används för att köra externa applikationer från NiKom, tex. Onlinespel\nLägg inte till den om den redan körs i s:user-startup eller på något annat sätt!!!")
				(command "run <>nil: l:fifo-handler")
			)
		  )
		)
		
	)
)

(procedure P_CopyReqFiles
	(
		(if (= (database "cpu") "68000")
		
			(
				(set cpu "68000")
			)
			
			(
				(set cpu "68020")
			)
		)

		(copyfiles
        	(prompt ("Vart ska NiKoms körbara (%s) filer kopieras till? (Måste installeras!)" cpu))
        	(dest ("%sbin" dirnamewslash))
        	(source ("bin/%s" cpu))
        	(pattern "#?")
        	(confirm)
        	(help "Dessa program _MÅSTE_ finnas för att köra NiKom.")
        )

		(copyfiles
			(prompt "Vart ska några användbara program för NiKom kopieras?")
			(dest ("%sbin" dirnamewslash))
			(source "bin")
			(pattern "(cryptpasswords|fixsysinfo|flushlibs|groupconv|InitNiKom)")
			(confirm)
			(help
				"Cryptpasswords - Används för att kryptera användarnas lösenord.\n"
				"Fixsysinfo - används för att fixa sysinfo.dat efter uppgradering - Ska inte behövas\n"
				"Flushlibs - Slänger ut alla bibliotek som finns i minnet men som inte används av något program.\n"
				"Groupconv - Konverterar grupp.dat, behövs inte i normala fall.\n"
				"Initnikom - För att rensa alla NiKoms alla datafiler och starta om BBSen. Körs automatiskt vid nyinstallation.\n"
			)
		)

		(copylib
			(prompt "För att filöverföringar ska fungera krävs det att xprzmodem.library "
			        "finns installerat i libs: i ditt system. Ska denna fil kopieras till "
			        "ditt libs:?")
			(help   "Filen är bara ca 20KB stor så den tar inte upp mycket plats. Det finns "
			        "även många andra program (som terminalprogram) som använder sig av "
	    		    "xprzmodem.library.\n"
			        "Det fullständiga arkivet av xprzmodem (med bla dokumentation) medföljer"
			        "ligger i libs katalogen.")
			(source "XprZmodem/xprzmodem.library")
			(dest   "libs:")
			(confirm)
		)

		(copylib
        	(prompt "Vart ska NiKom.library kopieras? i 99% av fallen är defaultvärdet korrekt")
        	(dest "LIBS:")
        	(source "libs/NiKom.library")
        	(confirm)
        	(help "Behövs inte...")
        )

		(copyfiles
	    	(prompt "Vart ska NiKoms dokumentationsfiler kopieras?")
	    	(dest ("%sdoc" dirnamewslash))
	    	(source "doc")
	    	(pattern "#?")
	    	(confirm)
	    	(help "Dessa filer innehåller all dokumentation för NiKom.")
		)

		(copyfiles
	    	(prompt "Vart ska NiKoms manualfiler kopieras? (Dessa är manualer som används i BBS:en alltså)")
	    	(dest ("%smanualer" dirnamewslash))
	    	(source "manualer")
	    	(pattern "#?")
	    	(confirm)
	    	(help "Dessa innehåller manualer för NiKom BBS:en, som användarna och sysop kan titta i.")
		)

	)
)

(procedure P_CreateReqDirectories
	(
		(working
			("Skapar bibliotek i %s..." dirname)
		)
		
		(makedir
			("%sDoc" dirnamewslash)
		)
		
		(makedir
			("%sDatocfg" dirnamewslash)
		)		

		(makedir
			("%sDatocfg/Areor" dirnamewslash)
		)		

		(makedir
			("%sManualer" dirnamewslash)
		)		

		(makedir
			("%sPrivat" dirnamewslash)
		)		

		(makedir
			("%sMoten" dirnamewslash)
		)

		(makedir
			("%sRexx" dirnamewslash)
		)

		(makedir
			("%sUsers" dirnamewslash)
		)

		(makedir
			("%sProgram" dirnamewslash)
		)

		(makedir
			("%sTempfiler" dirnamewslash)
		)
	)
)

(Procedure P_CryptedPasswords
	(
		(if (not
				(askchoice
					(prompt "Vill du använda krypterade lösenord? (läs hjälpen för viktig info!)")
					(help "Krypterade lösenord betyder att ingen kan se användarnas lösenord i klarttext, ger lite mer säkherhet och förhindrar också sysop att se alla användares lösenord.\n\nOBS! Glöm inte att köra programmet bin/cryptpasswords efter du startat upp servern!")
					(choices "JA" "NEJ")
					(default 0)
				)
		    )

			(
				(P_Addtoconfigfile "NiKom:Datocfg/System.cfg" "CRYPTEDPASSWORDS=JA")
			)
		
			(
				(P_Addtoconfigfile "NiKom:Datocfg/System.cfg" "CRYPTEDPASSWORDS=NEJ")
			)
		)
	)
)


; slut på funktioner som används från programmet.

; Checka så att användaren verkligen har WB 2.04 eller bättre.

(if (< (/ (getversion) 65536) 37)
(
    (abort "Du behöver Workbench 2.04 för att kunna använda NiKom!")
))

(set NiKomexists 1)

(message
	"Välkommen till installation av NiKom v1.60\n\n\n"
	"NiKom är © Tomas Kärki 1996-1998\n"
	"           Niklas Lindholm 1992-1996."
)

(set InstType
	(askchoice
		(prompt "Välj installationsmetod")
		(help
			"Välj det valet som passar ihop med din version av NiKom eller om du vill installera om på nytt.\n\n"
			"OBS! Finns inte din version med i listan så måste du uppgradera till någon av de versioner som finns med först!\n\n"
			"Det går alltså _inte_ att uppdatera direkt från NiKom v1.51 --> NiKom v1.60 som det är nu...\n"
			"Om du vill uppgradera från NiKom v1.51, kontakta Tomas Kärki!"
		)
		
		(choices
			"Helt ny installation."
			"Uppgradera från NiKom v1.60beta4"
		)
	)
)

(if (NOT
		(exists
			"NiKom:" (noreq)
		)
	)

	(	
		(set NiKomexists 0)
	)

	(
		(set dirname
			"NiKom:"
		)


		(if (NOT (= (substr dirname (- (strlen dirname) 1) 1) ":"))
		        (
		                (set dirnamewslash
        		                (cat dirname "/")
		                )
		        )
        
		        (
        			(set dirnamewslash dirname)
		        )
		)
	)
)

(select InstType
	; Nyinstallation
	(
		(if (= NiKomexists 1)
			(
				(if
					(askchoice
						(prompt "Varning! Du har redan NiKom: assignat, är du säker att du vill fortsätta den nya installationen?\n ALL DATA KOMMER ATT FÖRLORAS!")
						(help "Not yet")
						(choices "JA" "NEJ")
						(default 1)
					)
				
					(abort "Installationen avbruten...")
				
				)

			)
		)

		(set dirname
        	(askdir
            	    (prompt "Bibliotek att installera NiKom i. NiKom: kommer automatiskt att assignas till detta bibliotek.\nOBS! Programmet skapar inget bibliotek utan det måste du göra själv!")
                	(help @askdir-help)
                	(default "Work:")
	        )
		)

		(if (NOT (= (substr dirname (- (strlen dirname) 1) 1) ":"))
                (
                        (set dirnamewslash
                                (cat dirname "/")
                        )
                )
                
		        (
		        	(set dirnamewslash dirname)
                )
        )
        
        (makeassign "NiKom" ("%s" dirname))
        (P_CreateReqDirectories)
		(P_CopyReqFiles)
		(P_CopyConfigFiles)
        (run "bin/InitNiKom -q <>con:")
        (P_NewInstallation)
	)
	
	; Uppgradering från NiKom v1.60beta4
	(
		(if (NOT NiKomexists)
			(
				(abort
					"Kunde inte hitta någon föregående version av NiKom, avbryter installationen..."
				)
			)
		)
		
		(P_CopyReqFiles) ; Kopiera nödvändiga filer för uppgradering.
		
	)
)

	(set @default-dest dirname)


	(P_CryptedPasswords) ; Fråga användaren om han vill använda krypterade lösenord.

	(P_AddCommandstokommandoncfg) ; Addera valfria kommandon till kommandon.cfg

	(P_Endofinstallation) ; Do some stuff at the end of the installation.
