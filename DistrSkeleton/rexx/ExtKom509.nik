/* Kommandot Lista Innehåll
$VER: Lista Innehåll 1.47 (8.9.93)
N=LISTA INNEHÅLL
O=2
#=509
A=.
*/

parse arg nod pers argument .

options results

/* Här kan du själv definiera vilka arkivformat ditt system klarar av att
*  visa och vilket program som ska anropas med vilka argument. Glöm inte att
*  uppdatera 'antalgormat' när du lägger till arkivformat. */

ext.1 = '.LHA'; listprg.1 = 'LhA v'
ext.2 = '.LZH'; listprg.2 = 'LhA v'
ext.3 = '.ARC'; listprg.3 = 'pkax -v'
ext.4 = '.ZIP'; listprg.4 = 'UnZip -v'

antalformat = 4

/* Tack, det var allt */

if argument = "" then do
	'sendstring' '0d0a0a'x'Skriv: Lista Innehåll: <arkivnamn>' '0d0a'x
	exit
	end

'whicharea'
area=result

filnamn=FileInfo(argument,'n',area)
if filnamn=-1 then do
	'sendstring' '0d0a0a'x'Finns ingen sådan fil!' '0d0a'x
	exit
end

fil=AreaInfo(area,'d'||FileInfo(filnamn,'i',area))||filnamn

do i=1 to antalformat
	if upper(right(fil,length(ext.i))) = ext.i then leave
end

if i=antalformat+1 then do
	'sendstring' '0d0a0a'x'Detta program supportrar inte formatet' right(fil,4) '0d0a'x
	exit
end

parse var listprg.i prgnamn resten

cmdstring = prgnamn '>t:Liin'||nod resten fil
address command cmdstring

'sendtextfile t:Liin'||nod
address command 'delete t:Liin'||nod
exit
